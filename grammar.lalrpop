use ::std::str::FromStr;

use crate::ast::{span, Span};
use crate::ast::evolution::*;
use crate::ast::object::*;
use crate::ast::term::*;

grammar;

match {
    // Turn newlines into a token (they are skipped by default)
    r"(\r\n|\n|\r)(\s|\r|\n)*" => END,
} else {
    // Match string literals
    r#"("[^"]*"|([^"]*\\")*)"# => STR,
} else {
    // Ignore comments (only full-line for now)
    r#"\s*#[^\r\n]*(\r\n|\n|\r)(\s|\r|\n)*"# => { },
} else {
    // Ignore any other whitespace (except newlines above)
    r"\s+" => { },
} else {
    _
}

pub evolution: EvolutionAst = {
    END?
        "apivolve" <v:r"[0-9]+\.[0-9]+(\.[0-9]+)?"> END
        <ds:(<dependency>)*>
        <es:(<block>)*>
        END => EvolutionAst::new(v.to_owned(), ds, es),
};

dependency: Dependency = {
    "depends on" <p:STR> "dynamic" END => Dependency::dynamic(Path::new(p)),
//    "depends on" <p:path> "hash" <hash:"'[a-zA-Z0-9]+'"> END => Dependency::fixed(p, hash),
    "depends on" <p:STR> "hash" <hash:STR> END => Dependency::fixed(Path::new(p), hash),
};

block: Block = {
    "add" "object" <name:identifier> "{" END <fields:(<object_field>)*> "}" => Block::Obj(ObjectOp::Add(name, fields)),
    "change" "object" <name:identifier> "{" END <fields:(<object_field>)*> "}" => Block::Obj(ObjectOp::Add(name, fields)),
};

object_field: FieldOp = {
    "add" <name:identifier> <props:object_field_prop*> => FieldOp::Add(name, props),
    "change" <name:identifier> <props:object_field_prop*> => FieldOp::Change(name, props),
    "delete" <name:identifier> END => FieldOp::Delete(name),
}

object_field_prop: FieldProperty = {
    "type" <n:identifier> END? => FieldProperty::Type(n),
    "description" <l: @L> <d:STR> <r: @R> END? => FieldProperty::Description(d.to_owned(), span(l, r)),
    "default" <v:value> END? => FieldProperty::Default(Expression::Val(v)),
}

identifier: Iden = {
    <l: @L> <n:r"[a-zA-Z_][a-zA-Z0-9_]*"> <r: @R> => Iden::new(n.to_owned(), l, r),
};

//TODO @mark: support more types
value: Value = {
    <l: @L> <v:STR> <r: @R> => Value::Str(v.to_owned(), span(l, r)),
}
